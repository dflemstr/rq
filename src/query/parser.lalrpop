// -*- mode: rust -*-
use std::str::FromStr;

use query;
use value;

grammar;

sep<A, S>: Vec<A> = {
    <v:(<A> S)*> <e:A> => {
        let mut v = v;
        v.push(e);
        v
    }
};

pub query: query::Query = {
    sep<process, bar> => query::Query(<>)
};

process: query::Process = {
    <ident> <expr*> => query::Process(<>),
};

expr: query::Expression = {
    <ident> => query::Expression::Value(value::Value::String(<>)),
    <integer> => query::Expression::Value(value::Value::I64(<>)),
};

ident: String = r"[a-zA-Z_][a-zA-Z0-9_-]*" => <>.to_owned();
integer: i64 = {
    r"[0-9]+" => i64::from_str(<>).unwrap(),
    "-" <r"[0-9]+"> => -i64::from_str(<>).unwrap(),
};
bar: () = "|" => ();
